apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"

react {
    autolinkLibrariesWithApp()
}


def enableProguardInReleaseBuilds = false
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {
    ndkVersion rootProject.ext.ndkVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.shapebeats"
    defaultConfig {
        applicationId "com.shapebeats"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 10
        versionName "1.5.2"
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                cppFlags "-std=c++17"
            }
        }
    }
    
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }
    
    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
            version "3.22.1"
        }
    }
    
    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            universalApk true
        }
    }
    
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = false
        }
        abi {
            enableSplit = true
        }
    }
}

dependencies {
    implementation("com.facebook.react:react-android")

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("compileDebugJavaWithJavac") || task.name.contains("compileReleaseJavaWithJavac")) {
        task.doFirst {
            def entryPointFile = file("build/generated/autolinking/src/main/java/com/facebook/react/ReactNativeApplicationEntryPoint.java")
            if (entryPointFile.exists()) {
                def content = entryPointFile.text
                if (content.contains("LegacyArchitectureLogger")) {
                    content = content.replaceAll("import com\\.facebook\\.react\\.common\\.annotations\\.internal\\.LegacyArchitectureLogger;\\s*\n", "")
                    entryPointFile.write(content)
                }
            }

            def autolinkingCpp = file("build/generated/autolinking/src/main/jni/autolinking.cpp")
            if (autolinkingCpp.exists()) {
                def cppContent = autolinkingCpp.text
                if (cppContent.contains("RNSSafeAreaViewComponentDescriptor")) {
                    cppContent = cppContent.replace("RNSSafeAreaViewComponentDescriptor", "RNCSafeAreaViewComponentDescriptor")
                    autolinkingCpp.write(cppContent)
                }
            }
        }
    }
}
